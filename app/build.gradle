plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/spotless.gradle"

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.weather.app"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation" : "$projectDir/schemas".toString()]
                arguments += ["dagger.hilt.disableModulesHaveInstallInCheck" : "true"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

secrets {
    propertiesFileName 'secret.properties'
}

dependencies {

    implementation kotlinCore
    implementation support
    implementation fragment

    implementation dagger.libs
    kapt dagger.proc

    implementation lifecycle.libs

    implementation room.libs
    kapt room.proc

    implementation navigation.libs

    implementation retrofit
    implementation networking.libs

    implementation utils.libs
    kapt utils.proc
    implementation rxJava

    implementation "com.github.hadilq:live-event:1.3.0"
}
